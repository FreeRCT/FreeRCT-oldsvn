path=getDirectory("");

tilew=newArray(64, 128, 256);
slfac=0.25;
borderfac=8;
boffs=newArray(0, 0, 0);
dotdensity=4;

nlayouts=3;
simgs=newArray(nlayouts);
timgs=newArray(nlayouts);

//setBatchMode(true);
for (i=0; i<lengthOf(tilew); i++) {
	for (j=0; j<nlayouts; j++) {
		newImage("Temp1", "8-bit Black", tilew[i], tilew[i], 3*2);
		simgs[j]=getImageID();
	}
	for (c=0; c<3; c++) {
		for (o=0; o<2; o++) {
			selectImage(simgs[0]);
			setSlice(c*2+o+1);
			makeSele(tilew[i], slfac, 0, 0, 255, o, c);
			selectImage(simgs[1]);
			setSlice(c*2+o+1);
			makeSele(tilew[i], slfac, 0, 0, 255, o, c);
			twtmp=tilew[i]-2*tilew[i]/borderfac;
			twxoff=tilew[i]/borderfac;
			if (o==0) {
				twxoff+=0.5*tilew[i]/borderfac;
			} else {
				twxoff-=0.5*tilew[i]/borderfac;
			}
			twyoff=(boffs[c]+0.5)*tilew[i]/borderfac;
			makeSele(twtmp, slfac, twxoff, twyoff, 0, o, c);
			run("Select All");
			run("Copy");
			selectImage(simgs[2]);
			setSlice(c*2+o+1);
			run("Paste");
			makeSele(twtmp, slfac, twxoff, twyoff, 128, o, c);
			for (x=0; x<getWidth(); x++) {
				for (y=0; y<getHeight(); y++) {
					if (x%dotdensity==0 && y%dotdensity==0 && getPixel(x, y)==128) {
						setPixel(x, y, 255);
					}
				}
			}
			changeValues(0, 254, 0);
		}
	}
	for (j=0; j<nlayouts; j++) {
		selectImage(simgs[j]);
		run("Flip Vertically", "stack");
		newImage("Temp1", "8-bit Black", tilew[i]*2, tilew[i]*3, 1);
		timgs[j]=getImageID();
	}
	for (c=0; c<3; c++) {
		for (o=0; o<2; o++) {
			for (j=0; j<nlayouts; j++) {
				selectImage(simgs[j]);
				setSlice(c*2+o+1);
				run("Select All");
				run("Copy");
				setPasteMode("Copy");
				selectImage(timgs[j]);
				makeRectangle(o*tilew[i], c*tilew[i], tilew[i], tilew[i]);
				run("Paste");
			}
		}
	}
	for (j=0; j<nlayouts; j++) {
		selectImage(simgs[j]);
		close();
		selectImage(timgs[j]);
		if (j==0) {
			str="";
		} else {
			str=j;
		}
		saveAs("PNG", path+tilew[i]+"px"+str+".png");
		close();
	}
}

function makeSele(tilew, slfac, xoffs, yoffs, col, o, c) {
	slshift=tilew*slfac;
	xs=newArray(4);
	ys=newArray(4);
	xs[0]=tilew/2;
	xs[1]=tilew/2;
	ys[0]=0;
	ys[1]=slshift;
	ys[2]=tilew/4+slshift;
	ys[3]=tilew/4;
	if (o==0) {
		xs[2]=tilew;
		xs[3]=tilew;
	} else {
		xs[2]=0;
		xs[3]=0;
	}
	if (c==1) {
		ys[0]+=slshift;
	} else if (c==2) {
		ys[3]+=slshift;
	}
	for (i=0; i<lengthOf(xs); i++) {
		xs[i]+=xoffs;
		ys[i]+=yoffs;
	}
	makeSelection("polygon", xs, ys);
	setColor(col);
	getRawStatistics(area);
	if (area!=0) {
		fill();
	}
	run("Select None");
}
